defmodule Wocky.Events.UserInvitation do
  @moduledoc "Notification for a user inviting another user to be their friend"

  alias Wocky.Account.User
  alias Wocky.Contacts

  defstruct [
    :to,
    :from,
    :share_type
  ]

  @type t :: %__MODULE__{
          to: User.t(),
          from: User.t(),
          share_type: Contacts.share_type()
        }

  use ExConstructor
end

defimpl Wocky.Notifier.Push.Event, for: Wocky.Events.UserInvitation do
  import Wocky.Notifier.Push.Utils

  @impl true
  def notify?(_), do: true

  @impl true
  def recipient(%{to: to}), do: to

  @impl true
  def message(%{from: from} = _event) do
    get_handle(from) <> " wants to connect with you"
  end

  @impl true
  def uri(%{from: from} = _event), do: make_uri(:invitations, from.id)

  @impl true
  def ignore_block?(_event), do: false

  @impl true
  def opts(_), do: []
end

defimpl Wocky.Notifier.InBand.Event, for: Wocky.Events.UserInvitation do
  @impl true
  def notify?(_), do: true

  @impl true
  def event_type(_), do: :user_invitation

  @impl true
  def required_fields(_),
    do: [
      :other_user_id,
      :user_id,
      :share_type
    ]

  @impl true
  def transform(event),
    do: %{
      other_user_id: event.from.id,
      user_id: event.to.id,
      share_type: event.share_type
    }

  @impl true
  def ignore_block?(_event), do: false
end
